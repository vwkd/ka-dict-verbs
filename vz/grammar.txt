// grammar in McKeeman Form extended with
// comments, repetitions, optional, arguments, repetition index

Text
    Line NewlineLine*

NewlineLine
    nl Line

Line
    Source ws TargetOrReference

Source
    WordKaMeaning
    WordKa

WordKaMeaning
    WordKa SuperscriptNumber

WordKa
    WordKaHyphen
    CharsKa

// note: allow only single hyphen in word
WordKaHyphen
    CharsKa "-" CharsKa

CharsKa
    CharKa+

CharKa
    UNICODE_GEORGIAN_CHARACTER

// todo: maybe more?
SuperscriptNumber
    "¹"
    "²"
    "³"
    "⁴"
    "⁵"
    "⁶"
    "⁷"
    "⁸"
    "⁹"

TargetOrReference
    Reference
    Target

Reference
    TagsWhitespace? Type ws Source

TagsWhitespace
    Tags ws

Type
    "s." ws "Bed."
    "s." 
    "id."

Tags
    "{" Tag CommaWhitespaceTag* "}"

CommaWhitespaceTag
    "," ws Tag

Tag
    "biol."
    "bot."
    "chem."
    "chew."
    "desp."
    "elektr."
    "ethn."
    "fig."
    "gr."
    "gur."
    "hist."
    "HV."
    "imer."
    "ing."
    "iro."
    "jur."
    "kach."
    "khar."
    "khis."
    "landw."
    "letsch."
    "math."
    "med."
    "mil."
    "moch."
    "moral."
    "mthiul."
    "mus."
    "neg."
    "nz."
    "o-imer."
    "photogr."
    "phys."
    "poet."
    "pol."
    "psch."
    "ratsch."
    "rl."
    "spo."
    "tech."
    "thusch."
    "typ."
    "u-imer."
    "u-ratsch."
    "umg."
    "unk."
    "va."
    "vulg."

Target
    Values
    Value

Values
    IntegerDotWhitespaceValue(1) WhitespaceIntegerDotWhitespaceValue_i=2(i + 1)+

WhitespaceIntegerDotWhitespaceValue(i)
    ws IntegerDotWhitespaceValue(i)

IntegerDotWhitespaceValue(i)
    i "." ws Value

Value
    TagsWhitespace? Sentences

// todo: assumes sentences if and only if separated by comma, not yet true ❗️
Sentences
    Sentence CommaWhitespaceSentence*

CommaWhitespaceSentence
    "," ws Sentence

// todo: assume expanded all shorthands, has no (), od., /, ;, not yet true ❗️
// todo: sentence might contain WordKa ❗️
Sentence
    WordDe WhitespaceWordDe*

WhitespaceWordDe
    ws WordDe

WordDe
    WordDeBig
    WordDeSmall
    WordDeHyphen

// todo: allow WordDeSmall?
WordDeHyphen
    WordDeBig "-" WordDeBig

// note: require at least two letters
WordDeBig
    CharDeBig CharDeSmall+

// note: require at least two letters
WordDeSmall
    CharDeSmall{2,}

CharDeBig
    UNICODE_GERMAN_BIG_CHARACTER

CharDeSmall
    UNICODE_GERMAN_SMALL_CHARACTER

nl
    UNICODE_NEWLINE_CHARACTER

ws
    UNICODE_WHITESPACE_CHARACTER