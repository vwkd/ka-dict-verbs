// grammar in McKeeman Form extended with
// comments, repetitions, optional, arguments, repetition index

Text
    Line NewlineLine*

NewlineLine
    nl Line

Line
    Key ws Value

Key
    IndexVariant
    Index

IndexVariant
    Index SuperscriptNumber

// todo: maybe more?
SuperscriptNumber
    "¹"
    "²"
    "³"
    "⁴"
    "⁵"
    "⁶"
    "⁷"
    "⁸"
    "⁹"

Index
    WordKa ("-" WordKa)?

// note: allow only single hyphen in word
WordKa
    CharKa+

CharKa
    UNICODE_GEORGIAN_CHARACTER

Value
    Reference
    Definitions

Reference
    ReferenceValue
    ReferenceValueTagged

ReferenceValueTagged
    Tags ws ReferenceValue

ReferenceValue
    ReferenceDirect
    ReferenceMeaning
    ReferenceIdentical

ReferenceDirect
    "s." ws Key

ReferenceMeaning
    "s." ws "Bed." ws Key

ReferenceIdentical
    "id." ws Key

Tags
    "{" Categories "}"

Categories
    Category CommaWhitespaceCategory*

CommaWhitespaceCategory
    "," ws Category

Category
    "biol."
    "bot."
    "chem."
    "chew."
    "desp."
    "elektr."
    "ethn."
    "fig."
    "gr."
    "gur."
    "hist."
    "HV."
    "imer."
    "ing."
    "iro."
    "jur."
    "kach."
    "khar."
    "khis."
    "landw."
    "letsch."
    "math."
    "med."
    "mil."
    "moch."
    "moral."
    "mthiul."
    "mus."
    "neg."
    "nz."
    "o-imer."
    "photogr."
    "phys."
    "poet."
    "pol."
    "psch."
    "ratsch."
    "rl."
    "spo."
    "tech."
    "thusch."
    "typ."
    "u-imer."
    "u-ratsch."
    "umg."
    "unk."
    "va."
    "vulg."

Definitions
    DefinitionList
    Definition

DefinitionList
    DefinitionListItem(1) WhitespaceDefinitionListItem_i=2(i + 1)+

WhitespaceDefinitionListItem(i)
    ws DefinitionListItem(i)

DefinitionListItem(i)
    i "." ws Definition

Definition
    Entries
    EntriesTagged

EntriesTagged
    Tags ws Entries

// todo: assumes entries if and only if separated by comma, not yet true ❗️
Entries
    Entry CommaWhitespaceEntry*

CommaWhitespaceEntry
    "," ws Entry

// todo: not true, might have other WordsKa ❗️
Entry
    WordsDe

// todo: assume expanded all shorthands, has no (), od., /, ;, not yet true ❗️
// note: allow only single hyphen in word, note: in grammar is two words
WordsDe
    WordDe "-" WordDe
    WordDe WhitespaceWordDe*

WhitespaceWordDe
    ws WordDe

// note: require at least two letters
WordDe
    CharDeBig CharDeSmall+
    CharDeSmall{2,}

CharDeBig
    UNICODE_GERMAN_BIG_CHARACTER

CharDeSmall
    UNICODE_GERMAN_SMALL_CHARACTER

nl
    UNICODE_NEWLINE_CHARACTER

ws
    UNICODE_WHITESPACE_CHARACTER